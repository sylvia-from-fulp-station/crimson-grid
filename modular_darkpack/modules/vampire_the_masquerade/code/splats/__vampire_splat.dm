/datum/splat/vampire
	abstract_type = /datum/splat/vampire

	power_type = /datum/discipline
	COOLDOWN_DECLARE(passive_bp_drain_cooldown)
	COOLDOWN_DECLARE(check_masq_violating_cooldown)


/datum/splat/vampire/splat_life(seconds_per_tick)
	if(CONFIG_GET(flag/passive_bp_drain))
		if(COOLDOWN_FINISHED(src, passive_bp_drain_cooldown))
			owner.adjust_blood_pool(-1)
			COOLDOWN_START(src, passive_bp_drain_cooldown, CONFIG_GET(number/passive_bp_drain_timer))

	if(COOLDOWN_FINISHED(src, check_masq_violating_cooldown))
		if(HAS_TRAIT(owner, TRAIT_MASQUERADE_VIOLATING_FACE) && (iscarbon(owner) ? !(owner.obscured_slots & HIDEFACE) : TRUE))
			SEND_SIGNAL(owner, COMSIG_MASQUERADE_VIOLATION)

		if(HAS_TRAIT(owner, TRAIT_MASQUERADE_VIOLATING_EYES) && (iscarbon(owner) ? !(owner.obscured_slots & HIDEEYES) : TRUE))
			SEND_SIGNAL(owner, COMSIG_MASQUERADE_VIOLATION)

		COOLDOWN_START(src, check_masq_violating_cooldown, 1 TURNS)

	return

/datum/splat/vampire/proc/get_discipline_power(datum/discipline_power/discipline_power_type)
	RETURN_TYPE(/datum/discipline_power)

	return get_discipline(discipline_power_type::discipline)?.get_power(discipline_power_type)

/datum/splat/vampire/proc/get_discipline(discipline_type)
	RETURN_TYPE(/datum/discipline)

	return get_power(discipline_type)?.discipline

/mob/living/proc/get_discipline(discipline_type)
	RETURN_TYPE(/datum/discipline)

	var/datum/splat/vampire/vampire = get_splat_with_discipline(src)
	return vampire?.get_discipline(discipline_type)

/datum/splat/vampire/proc/get_discipline_dots(discipline_type)
	var/datum/discipline/discipline = get_discipline(discipline_type)
	if(!discipline)
		return 0
	return discipline.level

/mob/living/proc/get_discipline_dots(discipline_type)
	var/datum/splat/vampire/vampire = get_splat_with_discipline(src)
	var/dots = vampire?.get_discipline_dots(discipline_type)
	if(isnull(dots))
		return 0
	return dots

/datum/splat/vampire/get_power(power_type)
	RETURN_TYPE(/datum/action/discipline)

	for (var/datum/action/discipline/found_action as anything in powers)
		if (!istype(found_action.discipline, power_type))
			continue

		return found_action

/datum/splat/vampire/add_power(power_type, level)
	// Prevent duplicates
	if (get_power(power_type))
		return FALSE
	var/datum/discipline/new_discipline = new power_type(level)
	var/datum/action/discipline/adding_action = new new_discipline.action_type(owner, new_discipline)
	adding_action.Grant(owner)
	LAZYADD(powers, adding_action)
	return TRUE

/datum/splat/vampire/remove_power(power_type)
	var/datum/action/discipline/found_action = get_power(power_type)
	if (!found_action)
		return FALSE

	LAZYREMOVE(powers, found_action)
	qdel(found_action)
	return TRUE

/datum/splat/vampire/change_power_level(power_type, new_level)
	var/datum/action/discipline/found_action = get_power(power_type)
	if (!found_action)
		return FALSE

	found_action.discipline.set_level(new_level)
	found_action.refresh_power_display()
	return TRUE

/datum/splat/vampire/get_selected_power()
	RETURN_TYPE(/datum/action/discipline)

	return selected_power

/datum/splat/vampire/set_selected_power(slot)
	if (!slot)
		// Just try to unselect
		if (!get_selected_power())
			return FALSE
		get_selected_power().unselect(FALSE)
		return TRUE

	if (length(powers) < slot)
		return FALSE
	get_selected_power()?.unselect()
	selected_power = powers[slot]
	get_selected_power()?.select()

	return TRUE
