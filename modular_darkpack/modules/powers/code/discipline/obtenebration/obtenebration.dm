/datum/discipline/obtenebration
	name = "Obtenebration"
	desc = {"Controls the darkness around you.
● Shadow Play: Passive
●● Shroud of Night: Manipulation + Occult (difficulty 7)
●●● Arms of the Abyss: Manipulation + Occult (difficulty 7)
●●●● Black Metamorphosis: Manipulation + Courage (difficulty 7)
●●●●● Tenebrous Form: Passive"}
	icon_state = "obtenebration"
	clan_restricted = TRUE
	power_type = /datum/discipline_power/obtenebration
	signature_clan = VAMPIRE_CLAN_LASOMBRA

/datum/discipline/obtenebration/post_gain()
	. = ..()
	var/datum/action/ritual_drawing/mysticism/mystic = new()
	mystic.Grant(owner)
	mystic.level = level

/datum/discipline/obtenebration/post_loss()
	. = ..()
	for(var/datum/action/action as anything in owner.actions)
		if(istype(action, /datum/action/ritual_drawing/mysticism))
			qdel(action)

/datum/discipline_power/obtenebration
	name = "Obtenebration power name"
	desc = "Obtenebration power description"

	effect_sound = 'sound/effects/magic/voidblink.ogg'

/obj/item/ammo_casing/magic/tentacle/lasombra
	projectile_type = /obj/projectile/tentacle/lasombra
	icon_state = ""

/obj/projectile/tentacle/lasombra
	icon = 'icons/effects/beam.dmi'
	damage_type = BURN
	icon_state = "curse0"

/datum/discipline_power/obtenebration/shadow_play
	name = "Shadow Play"
	desc = "Manipulate shadows to block visibility."

	level = 1
	check_flags = DISC_CHECK_CONSCIOUS | DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE
	target_type = TARGET_TURF | TARGET_MOB | TARGET_OBJ | TARGET_SELF
	range = 7
	vitae_cost = 1

	multi_activate = TRUE
	duration_length = 1 SCENES
	cooldown_length = 1 TURNS
	frenzy_usable = FALSE

	var/list/shadows = list() // A list of all active shadows
	var/datum/action/clear_shadows/cbutton // The button to clear everything

/datum/discipline_power/obtenebration/shadow_play/activate(target)
	. = ..()
	var/atom/movable/new_shadow = new(target)
	new_shadow.set_light(discipline.level+2, -10) // Ideally, the shadows would be a special thing impenetrable by anyone but the user, but this works for now
	shadows += new_shadow

	if(!cbutton) // Grant the button if it doesn't exist
		cbutton = new(src)
		cbutton.Grant(owner)

	addtimer(CALLBACK(src, .proc/remove_shadow, new_shadow), duration_length) // 3 minute timer per shadow

/datum/discipline_power/obtenebration/shadow_play/proc/remove_shadow(atom/movable/old_shadow)
	if(old_shadow && (old_shadow in shadows)) // Check if shadow still exists
		shadows -= old_shadow
		qdel(old_shadow)

	if(!length(shadows) && cbutton) // Remove the button if there are no shadows left
		cbutton.Remove(owner)
		QDEL_NULL(cbutton)

/datum/discipline_power/obtenebration/shadow_play/proc/remove_all_shadows()
	for(var/atom/movable/all_shadows in shadows)
		qdel(all_shadows)
	shadows.Cut()

	if(cbutton)
		cbutton.Remove(owner)
		QDEL_NULL(cbutton)

/datum/discipline_power/obtenebration/shroud_of_night
	name = "Shroud of Night"
	desc = "Turn the shadows into appendages to pull your enemies."

	level = 2
	check_flags = DISC_CHECK_CONSCIOUS | DISC_CHECK_CAPABLE | DISC_CHECK_LYING | DISC_CHECK_IMMOBILE
	target_type = TARGET_MOB
	range = 7
	vitae_cost = 0

	aggravating = TRUE
	violates_masquerade = TRUE

	cooldown_length = 5 SECONDS

/datum/discipline_power/obtenebration/shroud_of_night/pre_activation_checks(atom/target)
	if(SSroll.storyteller_roll_datum(owner, difficulty = 7, applic_stats = list(STAT_MANIPULATION, STAT_OCCULT)))
		return TRUE
	return FALSE

/datum/discipline_power/obtenebration/shroud_of_night/activate(mob/living/target)
	. = ..()
	target.Stun(1 SECONDS)
	var/obj/item/ammo_casing/magic/tentacle/lasombra/casing = new (owner.loc)
	casing.fire_casing(target, owner, null, null, null, ran_zone(), 0,  owner)

/datum/discipline_power/obtenebration/arms_of_the_abyss
	name = "Arms of the Abyss"
	desc = "Use shadows as your arms to harm and grab others from afar."

	level = 3
	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE
	target_type = TARGET_TURF
	range = 7

	violates_masquerade = TRUE
	cooldown_length = 1 TURNS

	var/list/active_tentacles = list()
	var/aggro_mode = ABYSS_TENTACLE_MODE_AGGRESSIVE

/datum/discipline_power/obtenebration/arms_of_the_abyss/activate(atom/target)
	. = ..()
	var/turf/target_turf = get_turf(target)

	if(target_turf && target_turf.get_lumcount() <= 0.4)
		// Remove any existing tentacles first
		for(var/mob/living/basic/abyss_tentacle/T in active_tentacles)
			if(T && !QDELETED(T))
				T.release_grabbed_mob()
				qdel(T)
		active_tentacles.Cut()

		var/roll = SSroll.storyteller_roll_datum(owner, difficulty = 7, applic_stats = list(STAT_MANIPULATION, STAT_OCCULT), numerical = TRUE)
		var/has_action = !!(locate(/datum/action/aggro_mode) in owner.actions)

		if(!has_action)
			var/datum/action/aggro_mode/A = new(src)
			A.Grant(owner)

		// Create tentacles based on successes
		for(var/i in 1 to roll)
			var/mob/living/basic/abyss_tentacle/new_tentacle
			// For the first tentacle, use the target turf
			if(i == 1 && !target_turf.is_blocked_turf(exclude_mobs = TRUE))
				new_tentacle = new /mob/living/basic/abyss_tentacle(target_turf, owner)
			else
				// For additional tentacles, find nearby valid turfs
				var/list/open_turfs = list()
				for(var/turf/T in orange(3, target_turf))
					if(!T.is_blocked_turf(exclude_mobs = TRUE) && T.get_lumcount() <= 0.4)
						open_turfs += T
				if(open_turfs.len)
					new_tentacle = new /mob/living/basic/abyss_tentacle(pick(open_turfs), owner)

			// if we ended up making a new tentacle add it to our list and inherit set aggro_mode
			if(new_tentacle)
				new_tentacle.ai_controller?.set_blackboard_key(BB_ABYSS_TENTACLE_MODE, aggro_mode)
				active_tentacles += new_tentacle
	else
		to_chat(usr, span_warning("The area is too bright for the shadows to manifest!"))
		return FALSE

/datum/discipline_power/obtenebration/black_metamorphosis
	name = "Black Metamorphosis"
	desc = "Fuse with your inner darkness, gaining shadowy armor."

	level = 4
	check_flags = DISC_CHECK_CONSCIOUS | DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE
	vitae_cost = 2

	violates_masquerade = TRUE
// CRIMSON EDIT ADD START - Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude
	var/saved_brute_mod = 1
	var/saved_burn_mod = 1
	var/saved_aggravated_mod = 1
// CRIMSON EDIT ADD END - Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude
	toggled = TRUE
	duration_length = 999 SCENES

	var/activating = FALSE
	var/successful = FALSE

/datum/discipline_power/obtenebration/black_metamorphosis/pre_activation_checks()
	. = ..()
	if(activating) // Prevent multi-activation while the do_after is ongoing
		to_chat(owner, span_warning("You are already attempting to activate Black Metamorphosis!"))
		return FALSE

	if(owner.get_generation() >= 10)
		activating = TRUE
		to_chat(owner, span_warning("Your body starts to meld with the shadows..."))
		if(do_after(owner, 2 TURNS, timed_action_flags = (IGNORE_USER_LOC_CHANGE | IGNORE_TARGET_LOC_CHANGE | IGNORE_HELD_ITEM)))
			return TRUE
	else if(owner.get_generation() <= 9)
		activating = TRUE
		to_chat(owner, span_warning("Your body starts to rapidly meld with the shadows..."))
		if(do_after(owner, 1 TURNS, timed_action_flags = (IGNORE_USER_LOC_CHANGE | IGNORE_TARGET_LOC_CHANGE | IGNORE_HELD_ITEM)))
			return TRUE

/datum/discipline_power/obtenebration/black_metamorphosis/activate()
	. = ..()
	activating = FALSE
	var/roll = SSroll.storyteller_roll_datum(owner, difficulty = 7, applic_stats = list(STAT_MANIPULATION, STAT_COURAGE))
	switch(roll)
		if(ROLL_SUCCESS)
			successful = TRUE
// CRIMSON EDIT ADD Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude
			saved_brute_mod = owner.physiology.brute_mod  
			owner.physiology.brute_mod = 0.70
			saved_burn_mod = owner.physiology.burn_mod 
			owner.physiology.burn_mod = 2  
			saved_aggravated_mod= owner.physiology.aggravated_mod
			owner.physiology.aggravated_mod = 0.80
// CRIMSON EDIT ADD Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude

			animate(owner, color = "#000000", time = 1 SECONDS, loop = 1)
			to_chat(owner, span_green("You successfully fuse with the shadows!"))
		if(ROLL_FAILURE)
			to_chat(owner, span_warning("You fail to control the shadows!"))
			deactivate()
		if(ROLL_BOTCH)
			owner.apply_damage(60, BRUTE) // 2 levels of lethal damage on a botch
			to_chat(owner, span_danger("The shadows lash out at you as you fail to fuse with them!"))
			deactivate()

/datum/discipline_power/obtenebration/black_metamorphosis/deactivate()
	. = ..()
	if(!successful)
		return
	to_chat(owner, span_notice("The shadows fall away from your body."))
	playsound(owner.loc, 'sound/effects/magic/voidblink.ogg', 50, FALSE)
// CRIMSON EDIT ADD Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude
	owner.physiology.brute_mod = saved_brute_mod
	owner.physiology.burn_mod = saved_burn_mod 
	owner.physiology.aggravated_mod = saved_aggravated_mod
// CRIMSON EDIT ADD Reduces Strength of Obtenebration 4 to stop it being stronger than fortitude
	animate(owner, color = initial(owner.color), time = 1 SECONDS, loop = 1)

/datum/discipline_power/obtenebration/tenebrous_form
	name = "Tenebrous Form"
	desc = "Become a shadow and resist all but fire, sunlight, and magic!"

	level = 5
	check_flags = DISC_CHECK_CONSCIOUS | DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE | DISC_CHECK_LYING
	vitae_cost = 3
	duration_length = 999 SCENES
	toggled = TRUE

	violates_masquerade = TRUE

	cooldown_length = 1 TURNS
	var/activating = FALSE
	var/saved_brute_mod = 1
	var/saved_burn_mod = 1
	var/saved_aggravated_mod = 1
	var/saved_clone_mod = 1
	var/saved_stamina_mod = 1
	var/saved_brain_mod = 1
	var/saved_density

/datum/discipline_power/obtenebration/tenebrous_form/pre_activation_checks()
	. = ..()
	if(activating) // Prevent multi-activation while the do_after is ongoing
		to_chat(owner, span_warning("You are already attempting to activate Tenebrous Form!"))
		return FALSE

	// do_after timer based on generation; gen 9 and below can spend more BP per turn, so it activates faster
	if(owner.get_generation() >= 10)
		activating = TRUE
		to_chat(owner, span_warning("Your body slowly starts to turn into an inky blot of shadow..."))
		if(do_after(owner, 3 TURNS, timed_action_flags = (IGNORE_USER_LOC_CHANGE | IGNORE_TARGET_LOC_CHANGE | IGNORE_HELD_ITEM)))
			return TRUE
	else if(owner.get_generation() == 9)
		activating = TRUE
		to_chat(owner, span_warning("Your body starts to turn into an inky blot of shadow..."))
		if(do_after(owner, 2 TURNS, timed_action_flags = (IGNORE_USER_LOC_CHANGE | IGNORE_TARGET_LOC_CHANGE | IGNORE_HELD_ITEM)))
			return TRUE
	else if(owner.get_generation() <= 8)
		activating = TRUE
		to_chat(owner, span_warning("Your body rapidly starts to turn into an inky blot of shadow..."))
		if(do_after(owner, 1 TURNS, timed_action_flags = (IGNORE_USER_LOC_CHANGE | IGNORE_TARGET_LOC_CHANGE | IGNORE_HELD_ITEM)))
			return TRUE

/datum/discipline_power/obtenebration/tenebrous_form/activate()
	. = ..()
	activating = FALSE
	playsound(owner.loc, 'sound/effects/magic/voidblink.ogg', 50, FALSE)
	saved_brute_mod = owner.physiology.brute_mod
	owner.physiology.brute_mod = 0
	saved_burn_mod = owner.physiology.burn_mod
	owner.physiology.burn_mod = 2
	saved_aggravated_mod= owner.physiology.aggravated_mod
	owner.physiology.aggravated_mod = 0
	saved_stamina_mod = owner.physiology.stamina_mod
	owner.physiology.stamina_mod = 0
	saved_brain_mod = owner.physiology.brain_mod
	owner.physiology.brain_mod = 0
	animate(owner, color = "#000000", time = 1 SECONDS, loop = 1)

	ADD_TRAIT(owner, TRAIT_STUNIMMUNE, MAGIC_TRAIT)
	ADD_TRAIT(owner, TRAIT_PUSHIMMUNE, MAGIC_TRAIT)
	ADD_TRAIT(owner, TRAIT_NOBLOOD, MAGIC_TRAIT)
	ADD_TRAIT(owner, TRAIT_PACIFISM, MAGIC_TRAIT) // Can't physically attack while in this form
	//ADD_TRAIT(owner, TRAIT_MOVE_FLYING, MAGIC_TRAIT) // Flying to simulate being unaffected by gravity
	ADD_TRAIT(owner, TRAIT_PIERCEIMMUNE, MAGIC_TRAIT)	//Stops bullets from embedding and taser electrodes no longer connect
	owner.pass_flags |= (PASSDOORS | PASSTABLE | PASSSTRUCTURE) // Phase through doors & fences / tables / machines, dumpsters, barrels, lampposts


	saved_density = owner.density
	owner.density = FALSE

/datum/discipline_power/obtenebration/tenebrous_form/deactivate()
	. = ..()
	to_chat(owner, span_notice("You return to your normal form."))
	playsound(owner.loc, 'sound/effects/magic/voidblink.ogg', 50, FALSE)
	owner.physiology.brute_mod = saved_brute_mod
	owner.physiology.burn_mod = saved_burn_mod
	owner.physiology.aggravated_mod = saved_aggravated_mod
	owner.physiology.stamina_mod = saved_stamina_mod
	owner.physiology.brain_mod = saved_brain_mod
	animate(owner, color = initial(owner.color), time = 1 SECONDS, loop = 1)

	REMOVE_TRAIT(owner, TRAIT_STUNIMMUNE, MAGIC_TRAIT)
	REMOVE_TRAIT(owner, TRAIT_PUSHIMMUNE, MAGIC_TRAIT)
	REMOVE_TRAIT(owner, TRAIT_NOBLOOD, MAGIC_TRAIT)
	REMOVE_TRAIT(owner, TRAIT_PACIFISM, MAGIC_TRAIT)
	//REMOVE_TRAIT(owner, TRAIT_MOVE_FLYING, MAGIC_TRAIT)
	REMOVE_TRAIT(owner, TRAIT_PIERCEIMMUNE, MAGIC_TRAIT)	//Stops bullets from embedding and taser electrodes no longer connect
	owner.pass_flags &= ~(PASSDOORS | PASSTABLE | PASSSTRUCTURE)

	owner.density = saved_density

//ACTIONS

// Aggro mode control for Arms of the Abyss
/datum/action/aggro_mode
	name = "Tentacle Control"
	desc = "Switches the aggro mode of your Arms of the Abyss"
	button_icon = 'modular_darkpack/master_files/icons/hud/screen_gen.dmi'
	button_icon_state = "harm"
	var/current_mode = ABYSS_TENTACLE_MODE_AGGRESSIVE
	var/datum/discipline_power/obtenebration/arms_of_the_abyss/abyss_power

/datum/action/aggro_mode/New(Target)
	. = ..()
	abyss_power = Target
	current_mode = abyss_power.aggro_mode

/datum/action/aggro_mode/Trigger(mob/clicker, trigger_flags)
	. = ..()
	if(!.)
		return

	if(!owner || !isliving(owner))
		return

	var/mob/living/carbon/human/tentacle_owner = owner

	if(!istype(tentacle_owner))
		return

	var/list/options = list(
		ABYSS_TENTACLE_MODE_AGGRESSIVE = "Aggressive (grab and damage targets)",
		ABYSS_TENTACLE_MODE_CONTROL = "Control (grab and restrain without damage)",
		ABYSS_TENTACLE_MODE_PASSIVE = "Passive (don't attack or grab)"
	)

	var/select = tgui_input_list(tentacle_owner, "Select tentacle behaviour", "Tentacle Mode", options)
	if(!select || !tentacle_owner)
		return
	if(!abyss_power)
		return
	abyss_power.aggro_mode = select
	current_mode = select

	var/tentacles = 0
	for(var/mob/living/basic/abyss_tentacle/T in abyss_power?.active_tentacles)
		if(T && !QDELETED(T))
			var/was_passive = (T.ai_controller?.blackboard[BB_ABYSS_TENTACLE_MODE] == ABYSS_TENTACLE_MODE_PASSIVE)
			T.ai_controller?.set_blackboard_key(BB_ABYSS_TENTACLE_MODE, abyss_power.aggro_mode)
			tentacles++

			if(select == ABYSS_TENTACLE_MODE_PASSIVE && T.ai_controller?.blackboard[BB_ABYSS_TENTACLE_GRABBED])
				T.release_grabbed_mob()
			else if(was_passive && select != ABYSS_TENTACLE_MODE_PASSIVE)
				T.recently_released.Cut()

	if(tentacles)
		to_chat(tentacle_owner, span_notice("You set your tentacle[tentacles == 1 ? "" : "s"] to [select] mode."))
		update_button_icon()

/datum/action/aggro_mode/proc/update_button_icon()
	switch(current_mode)
		if(ABYSS_TENTACLE_MODE_AGGRESSIVE)
			button_icon_state = "harm"
		if(ABYSS_TENTACLE_MODE_CONTROL)
			button_icon_state = "grab"
		if(ABYSS_TENTACLE_MODE_PASSIVE)
			button_icon_state = "disarm"
	build_all_button_icons()

// Shadow removal button for Shadow Play
/datum/action/clear_shadows
	name = "Clear Shadows"
	desc = "Clears all currently active Shadow Play shadows"
	button_icon = 'icons/mob/effects/genetics.dmi'
	button_icon_state = "shadow_portal"
	var/datum/discipline_power/obtenebration/shadow_play/power

/datum/action/clear_shadows/New(Target)
	. = ..()
	power = Target

/datum/action/clear_shadows/Trigger(mob/clicker, trigger_flags)
	. = ..()
	if(!.)
		return
	if(!power)
		return FALSE
	power.remove_all_shadows()

